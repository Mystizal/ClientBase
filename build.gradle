buildscript {
    repositories {
        jcenter()
        maven { url = "https://lukflug.github.io/maven/" }
        maven { url = "https://repo.spongepowered.org/maven" }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

group = project.modGroup
version = project.modVersion

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

minecraft {
    version = project.forgeVersion
    runDir = 'run'
    mappings = project.mcpVersion
    coreMod = 'dev.zihasz.clientbase.mixin.MixinLoader'
    makeObfSourceJar = false
}

repositories {
    jcenter()
    mavenCentral()
    maven { url = 'https://repo.spongepowered.org/maven' }
    maven { url = 'https://jitpack.io/' }
}

dependencies {
    compile('org.spongepowered:mixin:0.7.4-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }
    compile('org.reflections:reflections:0.9.11') { exclude group: 'com.google.guava', module: 'guava' }
    compile group: 'net.jodah', name: 'typetools', version: '0.5.0'
    compile group: 'com.github.ZeroMemes', name: 'Alpine', version: '1.5'
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    compile group: 'club.minnced', name: 'java-discord-rpc', version: '2.0.1'
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) { exclude 'mcmod.info' }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

shadowJar {
    dependencies {
        include(dependency('org.spongepowered:mixin'))
        include(dependency('org.reflections:reflections'))
        include(dependency('net.jodah:typetools'))
        include(dependency('com.github.ZeroMemes:Alpine'))
        include(dependency('com.googlecode.json-simple:json-simple'))
        include(dependency('club.minnced:java-discord-rpc'))
        include(dependency('org.javassist:javassist'))
    }
    exclude 'dummyThing'
    exclude 'LICENSE.txt'
    classifier = 'release'
}

mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, 'mixins.clientbase.refmap.json'
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
        classpath = sourceSets.main.compileClasspath
    }
}

jar {
    manifest {
        attributes(
                'MixinConfigs': 'mixins.clientbase.json',
                'tweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'TweakOrder': 0,
                'FMLCorePluginContainsFMLMod': 'true',
                'FMLCorePlugin': 'dev.zihasz.clientbase.mixin.MixinLoader',
                'ForceLoadAsMod': 'true',
                'FMLAT': 'clientbase_at.cfg'
        )
    }
}

build.dependsOn(shadowJar)